grammar Euporia;

start : (note+|headLine) EOF;
note : headLine ref item+ rem? eol;
headLine : HEAD_LINE;
eol : NEWLINE;
ref : wordRefRanges (EQ wordRefRanges)? colon;
numRef : NUM (DOT NUM (DOT NUM)?)?;
colon : COLON;

wordRefRanges : wordRefRange (TILDE wordRefRange)*;
wordRefRange : (firstWordRef midWordRef* lastWordRef)|singleWordRef;
firstWordRef : sep? auth? numRef? anchor? wordRef;
midWordRef : sep? auth? numRef? wordRef;
lastWordRef : sep? auth? numRef? wordRef anchor?;
singleWordRef : sep? auth? numRef? anchor? wordRef anchor?;
anchor : L_BRAC wordRef+ R_BRAC;

sep : SEP|COLON;
wordRef : (PRE_PUNCT|L_CURL)? (WORD) (PUNCT|R_CURL|NOT|DOT|SEMICOLON|COMMA|COLON)? EXT_PUNCT?;
item : varId? (extTriples|type freeText?);
type : HASHTAG;
general : GENERAL;
freeText : token+;
token : (((PRE_PUNCT|L_CURL)? (WORD|NUM) (PUNCT|R_CURL|NOT|DOT|SEMICOLON|COMMA|COLON)? EXT_PUNCT?)|(SEP|COLON));
rem : REM_BEG token*;
extTriples : extTriple+;
extTriple : general? type? conds? subject (predicate object|predicateObjectList) cfrRef* biblRef* DOT;
subject : WORD;
predicateObjectList : predicate (object|objectList (SEMICOLON predicate objectList)*);
predicate : WORD;
objectList : object (COMMA object)*;
object : WORD;
//biblRef : BIBL? WORD NUM (UNDERSCORE NUM (DOT NUM)*)*;
biblRef : BIBL WORD (DOT WORD)* NUM (DASH NUM)? WORD? (UNDERSCORE NUM (DASH NUM)? WORD? (DOT NUM WORD? (DASH NUM WORD?)?)*)*;
cfrRef : CFR NUM (WORD)? (DASH NUM (WORD)?)? (WORD)*;

conds : cond+;
cond : openCond varId+ closeCond;
openCond : L_CURL;
varId : (not? VLTYPE vlId)|(not? VITYPE viId);
vlId : NUM UNDERSCORE NUM WORD?;
viId : NUM UNDERSCORE NUM WORD?;
not : NOT;
closeCond : R_CURL;
auth : PA|PL;

PA : 'Pa.';
PL : 'Pl.';
REM_BEG : '//';
HEAD_LINE : '*';
NUM : [0-9]+;
SEP : '...'|'…'|'/';
COLON : ':';
DASH : '-';
EQ : '=';
HASHTAG : '#'('A'..'Z'|'a'..'z'|'_')+;
GENERAL : '#ontology';
WORD : (LAT_WORD (UNDERSCORE (LAT_WORD|GRC_WORD))*|GRC_WORD (UNDERSCORE (GRC_WORD|LAT_WORD))*);
LAT_WORD : ('a'..'z'|'A'..'Z'|'á'|'à'|'ä'|'é'|'è'|'ë'|'í'|'ì'|'ï'|'ó'|'ò'|'ö'|'ú'|'ù'|'ü'|'ƀ'|'ð'|'’'|DASH)+;
GRC_WORD : ('\u0370'..'\u0386'|'\u0388'..'\u03ff'|'\u1f00'..'\u1fff'|'\u2019'|'·'|DASH|'&'('a'..'z')+';')+;
NEWLINE : ('\n'|'\r'|'\r\n')+;
UNDERSCORE : '_';
TILDE : '~';
L_BRAC : '[';
R_BRAC : ']';
L_CURL : '{';
R_CURL : '}';
NOT : '!';
PRE_PUNCT : '“'|'('|'<'|'‘'|'≺';
PUNCT : '?'|'-'|'’'|'”'|')'|'≻';
SEMICOLON : ';';
COMMA : ',';
DOT : '.';
//WS : (' '|'\t')->channel(HIDDEN);
WS : (' '|'\t')->skip;
EXT_PUNCT : '>'|'"'|'='|' ';
AT : '@';
VLTYPE : '@vl:';
VITYPE : '@vi:';
BIBL : '@bib:'|'@bibl:';
CFR : '@cfr:';
ANY : .+?;
